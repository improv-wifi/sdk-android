import com.vanniktech.maven.publish.AndroidSingleVariantLibrary
import com.vanniktech.maven.publish.SonatypeHost

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'com.vanniktech.maven.publish'
}

android {
    namespace = "com.wifi.improv"

    compileSdk 34

    defaultConfig {
        minSdk 21
        targetSdk 34

        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

mavenPublishing {
    configure(new AndroidSingleVariantLibrary("release", true, true))

    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)
    signAllPublications()

    coordinates("org.openhomefoundation.improv-wifi", "sdk-android", "${System.getenv('IMPROV_LIB_RELEASE')}")

    pom {
        name = "Android SDK for Improv Wi-Fi"
        description = "Easily detect and connect Improv Wi-Fi devices to the network on Android using BLE"
        inceptionYear = "2021"
        url = "https://github.com/improv-wifi/sdk-android"
        licenses {
            license {
                name = "Apache License, Version 2.0"
                url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                distribution = "http://www.apache.org/licenses/LICENSE-2.0.txt"
            }
        }
        developers {
            developer {
                id = "improv-wifi"
                name = "Improv Wi-Fi"
                url = "https://github.com/improv-wifi"
            }
        }
        scm {
            url = "https://github.com/improv-wifi/sdk-android"
            connection = "scm:git:git://github.com/improv-wifi/sdk-android.git"
            developerConnection = "scm:git:ssh://git@github.com/improv-wifi/sdk-android.git"
        }
    }
}